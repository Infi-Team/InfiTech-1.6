###########
# Mars ores
###########

# Since someone forget to put them into oreDictionary by default
oreDict.oreCopper.add(itemMarsCopper);
oreDict.oreTin.add(itemMarsTin);
oreDict.oreIron.add(itemMarsIron);

##################
# Machines Removal
##################

# Removing machines which arent used
minetweaker.remove(itemRefineryGC);
minetweaker.remove(itemCompressorGC);
minetweaker.remove(itemECompressorGC);
minetweaker.remove(itemCircuitFabricatorGC);

###################
# Compressed Ingots
###################

# Compressed Ingots are now made in Implossion Compressor
minetweaker.remove(itemCompressedCopper);
minetweaker.remove(itemCompressedTin);
minetweaker.remove(itemCompressedAlu);
minetweaker.remove(itemCompressedSteel);
minetweaker.remove(itemCompressedBronze);
minetweaker.remove(itemCompressedIron);
minetweaker.remove(itemCompressedDesh);
minetweaker.remove(itemCompressedMeteoricIron);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedCopper, itemDarkAsh], itemPlateCopper, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedTin, itemDarkAsh], itemPlateTin, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedAlu, itemDarkAsh], itemPlateAlu, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedSteel, itemDarkAsh], itemplateSteel, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedBronze, itemDarkAsh], itemPlateBronze, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedIron, itemDarkAsh], itemplateIron, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedDesh, itemDarkAsh], itemIngotDesh, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemCompressedMeteoricIron, itemDarkAsh], itemIngotMeteoricIron, 2);

########################
# Desh and Meteoric iron
########################

# Desh and Meteoric iron needs Blast Furnace to get smelted
furnace.remove(itemIngotDesh);
furnace.remove(itemIngotMeteoricIron);
furnace.remove(itemIngotGTDesh);
furnace.remove(itemIngotGTMeteoricIron);
modSupport.gregtech.blastFurnace.addRecipe(itemIngotDesh, itemUnrefinedDesh, null, 1500, 120, 2000);
modSupport.gregtech.blastFurnace.addRecipe(itemIngotMeteoricIron, itemRawMeteoricIron, null, 1500, 120, 1500);

############
# Silk Touch
############

# Just a little boost if you mine Desh or Meteors with Silk Touch
modSupport.ic2.macerator.addRecipe(itemUnrefinedDesh * 2, itemOreDesh);
modSupport.ic2.macerator.addRecipe(itemRawMeteoricIron * 2, itemOreMeteor);

#####################
# GC Heavy-Duty tools
#####################

# Making HD tools to follow the GregTech way of tool crafting
recipes.removeShaped(itemSwordHD);
recipes.removeShaped(itemPickaxeHD);
recipes.removeShaped(itemShovelHD);
recipes.removeShaped(itemHoeHD);
recipes.removeShaped(itemAxeHD);
recipes.removeShaped(itemHelmHD);
recipes.removeShaped(itemChestHD);
recipes.removeShaped(itemLegsHD);
recipes.removeShaped(itemBootsHD);
recipes.addShaped(itemSwordHD, [
	[null, itemCompressedMeteoricIron, null],
	[toolFile, itemCompressedMeteoricIron, toolHammer],
	[null, stickWood, null]]);
recipes.addShaped(itemPickaxeHD, [
	[itemCompressedMeteoricIron, itemIngotMeteoricIron, itemIngotMeteoricIron],
	[toolFile, stickWood, toolHammer],
	[null, stickWood, null]]);
recipes.addShaped(itemShovelHD, [
	[toolFile, itemCompressedMeteoricIron, toolHammer],
	[null, stickWood, null],
	[null, stickWood, null]]);
recipes.addShaped(itemHoeHD, [
	[itemCompressedMeteoricIron, itemIngotMeteoricIron, toolHammer],
	[toolFile, stickWood, null],
	[null, stickWood, null]]);
recipes.addShaped(itemAxeHD, [
	[itemCompressedMeteoricIron, itemIngotMeteoricIron, toolHammer],
	[itemCompressedMeteoricIron, stickWood, null],
	[toolFile, stickWood, null]]);
recipes.addShaped(itemHelmHD, [
	[itemCompressedMeteoricIron, itemCompressedMeteoricIron, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, toolHammer, itemCompressedMeteoricIron]]);
recipes.addShaped(itemChestHD, [
	[itemCompressedMeteoricIron, toolHammer, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, itemCompressedMeteoricIron, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, itemCompressedMeteoricIron, itemCompressedMeteoricIron]]);
recipes.addShaped(itemLegsHD, [
	[itemCompressedMeteoricIron, itemCompressedMeteoricIron, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, toolHammer, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, null, itemCompressedMeteoricIron]]);
recipes.addShaped(itemBootsHD, [
	[itemCompressedMeteoricIron, null, itemCompressedMeteoricIron],
	[itemCompressedMeteoricIron, toolHammer, itemCompressedMeteoricIron]]);

###############
# GC Desh Tools
###############

# Making Desh tools to follow the GregTech way of tool crafting
minetweaker.remove(itemSwordDesh);
minetweaker.remove(itemPickaxeDesh);
minetweaker.remove(itemShovelDesh);
minetweaker.remove(itemHoeDesh);
minetweaker.remove(itemAxeDesh);
minetweaker.remove(itemHelmDesh);
minetweaker.remove(itemChestDesh);
minetweaker.remove(itemLegsDesh);
minetweaker.remove(itemBootsDesh);
minetweaker.remove(itemStickDesh);
recipes.addShaped(itemSwordDesh, [
	[null, itemCompressedDesh, null],
	[toolFile, itemCompressedDesh, toolHammer],
	[null, stickDesh, null]]);
recipes.addShaped(itemPickaxeDesh, [
	[itemCompressedDesh, itemIngotDesh, itemIngotDesh],
	[toolFile, stickDesh, toolHammer],
	[null, stickDesh, null]]);
recipes.addShaped(itemShovelDesh, [
	[toolFile, itemCompressedDesh, toolHammer],
	[null, stickDesh, null],
	[null, stickDesh, null]]);
recipes.addShaped(itemHoeDesh, [
	[itemCompressedDesh, itemIngotDesh, toolHammer],
	[toolFile, stickDesh, null],
	[null, stickDesh, null]]);
recipes.addShaped(itemAxeDesh, [
	[itemCompressedDesh, itemIngotDesh, toolHammer],
	[itemCompressedDesh, stickDesh, null],
	[toolFile, stickDesh, null]]);
recipes.addShaped(itemHelmDesh, [
	[itemCompressedDesh, itemCompressedDesh, itemCompressedDesh],
	[itemCompressedDesh, toolHammer, itemCompressedDesh]]);
recipes.addShaped(itemChestDesh, [
	[itemCompressedDesh, toolHammer, itemCompressedDesh],
	[itemCompressedDesh, itemCompressedDesh, itemCompressedDesh],
	[itemCompressedDesh, itemCompressedDesh, itemCompressedDesh]]);
recipes.addShaped(itemLegsDesh, [
	[itemCompressedDesh, itemCompressedDesh, itemCompressedDesh],
	[itemCompressedDesh, toolHammer, itemCompressedDesh],
	[itemCompressedDesh, null, itemCompressedDesh]]);
recipes.addShaped(itemBootsDesh, [
	[itemCompressedDesh, null, itemCompressedDesh],
	[itemCompressedDesh, toolHammer, itemCompressedDesh]]);
	
###############
# Wafer recipes
###############

# Wafers are now made in Assembly Machine
modSupport.gregtech.assembler.addRecipe(itemWaferBasic * 3, itemGemDiamond, itemBoardAdvanced, 1600, 2);
modSupport.gregtech.assembler.addRecipe(itemWaferAdvanced, itemGemDiamond, itemProcessorBoard, 3200, 4);
modSupport.gregtech.assembler.addRecipe(itemWaferSolar * 9, itemGemDiamond, itemDustLapis * 9, 1600, 2);

####################
# Heavy-Duty Tweaks
####################
minetweaker.remove(itemPlateHD);
minetweaker.remove(itemPlateHDT2);

###############
# Nailed plates
###############

# New component to make Heavy Duty plates
recipes.addShaped(itemPlateNailed, [[itemBoltStainless, toolHammer, itemBoltStainless], [itemCompressedBronze, itemCompressedAlu, itemCompressedSteel], [itemBoltStainless, null, itemBoltStainless]]);
recipes.addShaped(itemPlateNailedT2, [[null, itemBoltTungsten, itemBoltTungsten], [toolHammer, itemPlateHD, itemCompressedMeteoricIron], [null, itemBoltTungsten, itemBoltTungsten]]);

####################
# Heavy-Duty recipes
####################

# Heavy Duty (both Tiers) are made also in Implosion Compressor
modSupport.gregtech.implosionCompressor.addRecipe([itemPlateHD, itemDustTinyStainless * 2],  itemPlateNailed, 2);
modSupport.gregtech.implosionCompressor.addRecipe([itemPlateHDT2, itemDustTinyTungsten * 2],  itemPlateNailedT2, 2);

###########
# Canisters
###########

# Changing ingots to plates for Canisters
recipes.removeShaped(itemCanTin);
recipes.removeShaped(itemCanCopper);
recipes.addShaped(itemCanTin * 2, [
	[plateTin, null, plateTin],
	[plateTin, toolHammer, plateTin],
	[plateTin, plateTin, plateTin]]);
recipes.addShaped(itemCanCopper * 2, [
	[plateCopper, null, plateCopper],
	[plateCopper, toolHammer, plateCopper],
	[plateCopper, plateCopper, plateCopper]]);
	
##############
# Oil Refining
##############

# Just a little support to Refine Oil somewhere else with the Fuel Canister
modSupport.gregtech.distillationTower.addRecipe([itemCanFuel.withDamage(1), itemCellSulfuric, null, itemCellMethane], itemCanOil.withDamage(1), 2, 800, 32);